import {
  core.rune {load-rune},
  core.text {_get-content-pointer, _get-rune, _take-N, is-empty, take-N},
  core.text.show {show-text},
  this.error {Error, error, report},
  this.internal {_get-new-point, _load-text-offset, _proceed, _read, _rewind-state, _set-gap-spot},
  this.point {Point, point},
  this.stash {
    _Stash,
    _empty-stash,
    _join-stash,
    _set-expected,
    _stash,
  },
  this.state {State, new-state, state},
  this.tag {Label, Raw-Text, tag},
}

data parser(a) {
| _Parser((input: &cell(state)) -> either(_stash, a))
}

inline _unwrap<a>(p: parser(a)): (input: &cell(state)) -> either(_stash, a) {
  let _Parser(f) = p in
  f
}

inline zonk<a, b>(p: parser(a), k: (a) -> parser(b)): parser(b) {
  _Parser(function (st-ref: &cell(state)) {
    try v1 = _unwrap(p)(st-ref) in
    _unwrap(k(v1))(st-ref)
  })
}

inline return<a>(x: a): parser(a) {
  _Parser(function (_: &cell(state)) {
    Right(x)
  })
}

inline _get-state(): parser(&cell(state)) {
  _Parser(function (st-ref: &cell(state)) {
    Right(st-ref)
  })
}

inline get-point(): parser(point) {
  _Parser(function (st-ref: &cell(state)) {
    tie State of {cursor} = _read(st-ref) in
    Right(*cursor)
  })
}

inline run<a>(p: parser(a), st: &cell(state)): either(error, a) {
  let parse-result = _unwrap(p)(st) in
  tie State of {stream, cursor, gap-spot} = _read(st) in
  match parse-result {
  | Right(v) =>
    Right(v)
  | Left(e) =>
    let _Stash of {expected} = e in
    Left(
      Error of {
        found = show-text(take-N(*gap-spot, stream)),
        expected = expected(),
        error-location = *cursor,
      },
    )
  }
}

inline _run<a>(p: parser(a), st: &cell(state)): either(_stash, a) {
  _unwrap(p)(st)
}

define fail<a>(expected: list(tag)): parser(a) {
  _Parser(function (_: &cell(state)) {
    Left(_Stash of {
      expected = {
        function () {
          expected
        }
      },
    })
  })
}

constant end-of-input: parser(unit) {
  _Parser(function (st-ref: &cell(state)) {
    tie State of {stream} = _read(st-ref) in
    let len = core.text.text-byte-length(stream) in
    if eq-int(len, 0) {
      Right(Unit)
    } else {
      _set-gap-spot(st-ref, 1);
      Left(_Stash of {
        expected = {
          function () {
            [Label("end of input")]
          }
        },
      })
    }
  })
}

inline satisfy(p: (rune) -> bool): parser(rune) {
  _Parser(function (st-ref: &cell(state)) {
    tie State of {stream, cursor = Point of {row, column}} = _read(st-ref) in
    let len = core.text.text-byte-length(stream) in
    if eq-int(len, 0) {
      Left(_empty-stash)
    } else {
      let r = _get-rune(stream, 0) in
      if p(r) {
        let new-point = _get-new-point(r, *row, *column) in
        let offset = core.rune.size(r) in
        _proceed(st-ref, offset, new-point);
        Right(r)
      } else {
        _set-gap-spot(st-ref, 1);
        Left(_Stash of {
          expected = {
            function () {
              []
            }
          },
        })
      }
    }
  })
}

constant any-rune: parser(rune) {
  satisfy(function (_) {
    True
  })
}

inline chunk(t: &text): parser(unit) {
  _Parser(function (st-ref: &cell(state)) {
    tie State of {stream, cursor = Point of {row = orig-row, column = orig-column}} = _read(st-ref) in
    let len1 = core.text.text-byte-length(stream) in
    let len2 = core.text.text-byte-length(t) in
    if lt-int(len1, len2) {
      Left(_empty-stash)
    } else {
      let ptr1 = _get-content-pointer(stream) in
      let ptr2 = _get-content-pointer(t) in
      let f =
        define consumer(index: int, char-len: int, row: int, column: int): either(_stash, unit) {
          if eq-int(index, len2) {
            _proceed(st-ref, len2, Point of {row, column});
            Right(Unit)
          } else {
            let rune1 = load-rune(add-int(ptr1, index)) in
            let rune2 = load-rune(add-int(ptr2, index)) in
            let offset = core.rune.size(rune1) in
            if eq-rune(rune1, rune2) {
              let Point of {row, column} = _get-new-point(rune1, row, column) in
              consumer(add-int(index, offset), add-int(char-len, 1), row, column)
            } else {
              _set-gap-spot(st-ref, add-int(char-len, 1));
              Left(_Stash of {
                expected = {
                  function () {
                    [Raw-Text(t)]
                  }
                },
              })
            }
          }
        }
      in
      f(0, 0, *orig-row, *orig-column)
    }
  })
}

inline take-while(p: (rune) -> bool): parser(text) {
  _Parser(function (st-ref: &cell(state)) {
    tie State of {stream, cursor = Point of {row, column}} = _read(st-ref) in
    let len = core.text.text-byte-length(stream) in
    let ptr1 = _get-content-pointer(stream) in
    let f =
      define consumer(index: int, row: int, column: int): text {
        let is-tail = ge-int(index, len) in
        if is-tail {
          let result = _take-N(index, stream) in
          _proceed(st-ref, index, Point of {row, column});
          result
        } else {
          let r = load-rune(add-int(ptr1, index)) in
          if p(r) {
            let offset = core.rune.size(r) in
            let Point of {row, column} = _get-new-point(r, row, column) in
            consumer(add-int(index, offset), row, column)
          } else {
            let result = _take-N(index, stream) in
            _proceed(st-ref, index, Point of {row, column});
            result
          }
        }
      }
    in
    Right(f(0, *row, *column))
  })
}

inline drop-while(p: (rune) -> bool): parser(unit) {
  _Parser(function (st-ref: &cell(state)) {
    tie State of {stream, cursor = Point of {row, column}} = _read(st-ref) in
    let len = core.text.text-byte-length(stream) in
    let ptr1 = _get-content-pointer(stream) in
    let f =
      define consumer(index: int, row: int, column: int): unit {
        let is-tail = ge-int(index, len) in
        if is-tail {
          _proceed(st-ref, index, Point of {row, column});
          Unit
        } else {
          let r = load-rune(add-int(ptr1, index)) in
          if p(r) {
            let offset = core.rune.size(r) in
            let Point of {row, column} = _get-new-point(r, row, column) in
            consumer(add-int(index, offset), row, column)
          } else {
            _proceed(st-ref, index, Point of {row, column});
            Unit
          }
        }
      }
    in
    f(0, *row, *column);
    Right(Unit)
  })
}

constant ascii-space: parser(unit) {
  drop-while(
    function (r) {
      if eq-rune(r, ` `) {
        True
      } else-if ule-int32(sub-int32(magic cast(rune, int32, r), 0x9), 0x4) {
        True
      } else {
        False
      }
    },
  )
}

inline label<a>(l: &text, p: parser(a)): parser(a) {
  _Parser(function (st-ref: &cell(state)) {
    tie State of {stream} = _read(st-ref) in
    let base-offset = _load-text-offset(stream) in
    match _unwrap(p)(st-ref) {
    | Right(result) =>
      Right(result)
    | Left(e) =>
      let new-offset = _load-text-offset(stream) in
      if eq-int(base-offset, new-offset) {
        Left(_set-expected(
          function () {
            [Label(l)]
          },
          e,
        ))
      } else {
        Left(e)
      }
    }
  })
}

inline hidden<a>(p: parser(a)): parser(a) {
  _Parser(function (st-ref: &cell(state)) {
    match _unwrap(p)(st-ref) {
    | Right(result) =>
      Right(result)
    | Left(e) =>
      Left(_set-expected(
        function () {
          []
        },
        e,
      ))
    }
  })
}

inline attempt<a>(p: parser(a)): parser(a) {
  _Parser(function (st-ref: &cell(state)) {
    tie State of {stream, cursor = Point of {row, column}} = _read(st-ref) in
    let base-offset = _load-text-offset(stream) in
    match _unwrap(p)(st-ref) {
    | Right(result) =>
      Right(result)
    | Left(e) =>
      tie State of {gap-spot} = _read(st-ref) in
      mutate(st-ref, _rewind-state(base-offset, *row, *column, *gap-spot));
      Left(e)
    }
  })
}

inline look-ahead<a>(p: parser(a)): parser(a) {
  _Parser(function (st-ref: &cell(state)) {
    tie State of {stream, cursor = Point of {row, column}, gap-spot} = _read(st-ref) in
    let base-offset = _load-text-offset(stream) in
    match _unwrap(p)(st-ref) {
    | Right(result) =>
      mutate(st-ref, _rewind-state(base-offset, *row, *column, *gap-spot));
      Right(result)
    | Left(e) =>
      Left(e)
    }
  })
}

inline not-followed-by<a>(p: parser(a)): parser(unit) {
  _Parser(function (st-ref: &cell(state)) {
    tie State of {stream, cursor = Point of {row, column}, gap-spot} = _read(st-ref) in
    let base-offset = _load-text-offset(stream) in
    match _unwrap(p)(st-ref) {
    | Right(_) =>
      mutate(st-ref, _rewind-state(base-offset, *row, *column, *gap-spot));
      let head-char = take-N(1, stream) in
      let b on head-char = is-empty(head-char) in
      let _ = head-char in
      if b {
        Left(_empty-stash)
      } else {
        _set-gap-spot(st-ref, 1);
        Left(_Stash of {
          expected = {
            function () {
              []
            }
          },
        })
      }
    | Left(_) =>
      mutate(st-ref, _rewind-state(base-offset, *row, *column, *gap-spot));
      Right(Unit)
    }
  })
}

define _choice<a>(
  candidates: list(parser(a)),
  fallback: parser(a),
  _stash-cand: _stash,
  default-gap-spot: int,
): parser(a) {
  _Parser(function (st-ref: &cell(state)) {
    tie State of {stream} = _read(st-ref) in
    let old-offset = _load-text-offset(stream) in
    match candidates {
    | Nil =>
      match _unwrap(fallback)(st-ref) {
      | Right(v) =>
        _set-gap-spot(st-ref, default-gap-spot);
        Right(v)
      | Left(e) =>
        let new-offset = _load-text-offset(stream) in
        if eq-int(old-offset, new-offset) {
          Left(_join-stash(_stash-cand, e))
        } else {
          Left(e)
        }
      }
    | Cons(cand, rest) =>
      match _unwrap(cand)(st-ref) {
      | Right(v) =>
        _set-gap-spot(st-ref, default-gap-spot);
        Right(v)
      | Left(e) =>
        let new-offset = _load-text-offset(stream) in
        if eq-int(old-offset, new-offset) {
          _unwrap(_choice(rest, fallback, _join-stash(_stash-cand, e), default-gap-spot))(st-ref)
        } else {
          Left(e)
        }
      }
    }
  })
}

inline choice<a>(candidates: list(parser(a)), fallback: parser(a)): parser(a) {
  match candidates {
  | Nil =>
    fallback
  | Cons(p, rest) =>
    _Parser(function (st-ref) {
      tie State of {stream, gap-spot} = _read(st-ref) in
      let old-offset = _load-text-offset(stream) in
      match _unwrap(p)(st-ref) {
      | Right(v) =>
        Right(v)
      | Left(e) =>
        let new-offset = _load-text-offset(stream) in
        if eq-int(old-offset, new-offset) {
          _unwrap(_choice(rest, fallback, e, *gap-spot))(st-ref)
        } else {
          Left(e)
        }
      }
    })
  }
}

inline many<a>(p: parser(a)): parser(list(a)) {
  let f =
    define self() {
      with zonk {
        bind st = _get-state() in
        match _unwrap(p)(st) {
        | Right(v) =>
          with zonk {
            bind vs = self() in
            return(Cons(v, vs))
          }
        | Left(_) =>
          return([])
        }
      }
    }
  in
  f()
}

inline some<a>(p: parser(a)): parser(list(a)) {
  with zonk {
    bind v = p in
    bind vs = many(p) in
    return(Cons(v, vs))
  }
}

define zen(): unit {
  let my-parser =
    choice of {
      candidates = {
        [
          chunk("gaoe"),
          choice([chunk("k")], label("whatever", with zonk {
            bind _ = chunk("px") in
            bind _ = chunk("kgあ") in
            return(Unit)
          })),
          chunk("tあい"),
          label("whatever 2", with zonk {
            bind _ = chunk("pt") in
            bind _ = chunk("あhebc") in
            return(Unit)
          }),
          with zonk {
            bind _ = chunk("pt") in
            bind _ = chunk("あhebc") in
            return(Unit)
          },
          chunk("abc"),
          chunk("{}"),
          end-of-input,
        ]
      },
      fallback = chunk("tn"),
    }
  in
  let st = new-state(*"pqabcd\nctああhehoge \n\r  hehehetest") in
  let _ on st =
    let _ = _unwrap(chunk("abcd\nc"))(st) in
    let result = run(my-parser, st) in
    tie State of {stream, cursor = Point of {row, column}} = _read(st) in
    match result {
    | Right(_) =>
      print("remaining text: ");
      print(stream);
      print("\n");
      printf("0-indexed cursor point: ({}, {})\n", [%ld(*row), %ld(*column)]);
      let _ = st in
      print("pass!\n")
    | Left(e) =>
      printf("{}\n", [report(e)])
    }
  in
  let _ = st in
  Unit
}
