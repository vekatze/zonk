import {
  core.text {text-byte-length},
  core.text.entity,
  noa.gen.pairs {pairs},
  noa.gen.texts {texts},
  noa.suite {noa},
  this.internal {_read},
  this.parser {choice, chunk, end-of-input, many, parser, return, run, some, zonk},
  this.state {State, new-state},
}

define main(): unit {
  noa::check(
    "chunk(t1) against append(t1, t2) must succeed and the remaining text is t2",
    pairs(texts, texts),
    function (p) {
      let Pair(t1, t2) = p in
      let full-text on t1, t2 = core.text.append(t1, t2) in
      let st = new-state(full-text) in
      let parse-result on t1, t2, st =
        let p = chunk(t1) in
        let parse-result = run(p, st) in
        match parse-result {
        | Right(_) =>
          let result =
            tie State of {stream} = _read(st) in
            core.text.entity.as-entity::identity(t2, stream)
          in
          result
        | Left(_) =>
          False
        }
      in
      let _ = t1 in
      let _ = t2 in
      let _ = st in
      parse-result
    },
  );
  noa::check(
    "`t1` can be parsed by (chunk(t1) → end-of-input)",
    texts,
    function (t) {
      let st = new-state(t) in
      let parse-result on t, st =
        let p =
          with zonk {
            bind _ = chunk(t) in
            bind _ = end-of-input in
            return(Unit)
          }
        in
        match run(p, st) {
        | Right(_) =>
          True
        | Left(_) =>
          False
        }
      in
      let _ = t in
      let _ = st in
      parse-result
    },
  );
  noa::check(
    "`append(t1, t2)` can be parsed by `chunk(t1) → chunk(t2) → end-of-input`",
    pairs(texts, texts),
    function (p) {
      let Pair(t1, t2) = p in
      let full-text on t1, t2 = core.text.append(t1, t2) in
      let st = new-state(full-text) in
      let result on t1, t2, st =
        let p =
          with zonk {
            bind _ = chunk(t1) in
            bind _ = chunk(t2) in
            bind _ = end-of-input in
            return(Unit)
          }
        in
        match run(p, st) {
        | Right(_) =>
          True
        | Left(_) =>
          False
        }
      in
      let _ = t1 in
      let _ = t2 in
      let _ = st in
      result
    },
  );
  noa::check(
    "for any non-empty text, end-of-input must fail",
    texts,
    function (t) {
      let len on t = text-byte-length(t) in
      if eq-int(len, 0) {
        True
      } else {
        let st = new-state(t) in
        let result on st =
          match run(end-of-input, st) {
          | Right(_) =>
            False
          | Left(_) =>
            True
          }
        in
        let _ = st in
        result
      }
    },
  )
  // ...
}

constant sample-parser: parser(unit) {
  // construct a parser
  with zonk {
    // accepts: foo
    bind _ = chunk("foo") in
    // accepts: (buz|test|bar)
    bind _ = choice([chunk("buz"), chunk("test")], chunk("bar")) in
    // accepts: (qux)+
    bind _ = some(chunk("qux")) in
    // accepts: (yo)*
    bind _ = many(chunk("yo")) in
    // accepts: end-of-input
    bind _ = end-of-input in
    return(Unit)
  }
}

define zen(): unit {
  // construct an input
  let some-input = new-state(*"foobarquxqux") in
  let result on some-input =
    // run the parser
    match run(sample-parser, some-input) {
    | Right(_) =>
      print("pass\n")
    | Left(_) =>
      print("fail\n")
    }
  in
  let _ = some-input in
  result
}
