import {
  core.text {_drop-N, text-byte-length},
  noa.gen.pairs {pairs},
  noa.gen.texts {texts},
  noa.suite {Trope, noa},
  this.move.make-zonk-kit {make-zonk-kit},
  this.move.parse {choice, chunk, end-of-input, many, some, zonk},
  this.move.private.state,
  this.rule.zonk-kit {_Zonk-Kit},
}

define main(): unit {
  let Trope of {check = !check} = noa in
  !check(
    "chunk(t1) against append(t1, t2) must succeed and the remaining text is t2",
    pairs(texts, texts),
    function (p) {
      let Pair(t1, t2) = p in
      let full-text on t1, t2 = core.text.append(t1, t2) in
      pin k = make-zonk-kit(full-text) in
      let parse-result on t1, t2 =
        let p = chunk(t1) in
        match p(k) {
        | Right(_) =>
          tie _Zonk-Kit of {input-stream} = k in
          let cursor = this.move.private.state._get-cursor(k) in
          pin stream' = _drop-N(input-stream, cursor) in
          let result = eq-text(t2, stream') in
          result
        | Left(_) =>
          False
        }
      in
      let _ = t1 in
      let _ = t2 in
      parse-result
    },
  );
  !check(
    "`t1` can be parsed by (chunk(t1) → end-of-input)",
    texts,
    function (!t) {
      pin k = make-zonk-kit(!t) in
      pin t = !t in
      let parse-result =
        let p: zonk(unit) =
          function (k) {
            try _ = chunk(t)(k) in
            try _ = end-of-input(k) in
            Right(Unit)
          }
        in
        match p(k) {
        | Right(_) =>
          True
        | Left(_) =>
          False
        }
      in
      parse-result
    },
  );
  !check(
    "`append(t1, t2)` can be parsed by `chunk(t1) → chunk(t2) → end-of-input`",
    pairs(texts, texts),
    function (p) {
      let Pair(t1, t2) = p in
      let full-text on t1, t2 = core.text.append(t1, t2) in
      pin k = make-zonk-kit(full-text) in
      let result on t1, t2 =
        let p: zonk(unit) =
          function (k) {
            try _ = chunk(t1)(k) in
            try _ = chunk(t2)(k) in
            try _ = end-of-input(k) in
            Right(Unit)
          }
        in
        match p(k) {
        | Right(_) =>
          True
        | Left(_) =>
          False
        }
      in
      let _ = t1 in
      let _ = t2 in
      result
    },
  );
  !check(
    "for any non-empty text, end-of-input must fail",
    texts,
    function (t) {
      let len on t = text-byte-length(t) in
      if eq-int(len, 0) {
        True
      } else {
        pin k = make-zonk-kit(t) in
        let result =
          match end-of-input(k) {
          | Right(_) =>
            False
          | Left(_) =>
            True
          }
        in
        result
      }
    },
  )
  // ...
}

inline _sample-parser: zonk(unit) {
  // construct a parser
  function (k) {
    // accepts: foo
    try _ = chunk("foo")(k) in
    // accepts: (buz|test|bar)
    try _ = choice("yo", [chunk("buz"), chunk("test")], chunk("bar"))(k) in
    // accepts: (qux)+
    try _ = some(chunk("qux"))(k) in
    // accepts: (yo)*
    try _ = many(chunk("yo"))(k) in
    // accepts: end-of-input
    try _ = end-of-input(k) in
    Right(Unit)
  }
}

define zen(): unit {
  pin k = make-zonk-kit(*"foobarquxquxyoyoyoyo") in
  match _sample-parser(k) {
  | Right(_) =>
    print("pass\n")
  | Left(_) =>
    print("fail\n")
  }
}
