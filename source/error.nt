import {
  core.list {is-empty},
  core.text {format, intercalate, take-N},
  core.text.show {show-text, show-text-N},
  this.pos {Pos, pos},
}

data error {
| Error(
    expected: list(&text),
  )
}

define unexpected-end-of-input(): error {
  Error of {expected = []}
}

define show-found(size: int, stream: &text): text {
  show-text(take-N(size, stream))
}

define show-position(p: pos): text {
  let Pos of {row, column} = p in
  format("row {}, column {}", [%ld(add-int(row, 1)), %ld(add-int(column, 1))])
}

define set-expected(o: &text, e: error): error {
  match e {
  | Error of {} =>
    Error of {expected = [o]}
  }
}

inline join(e1: error, e2: error): error {
  match e1, e2 {
  | Error(expected1), Error(expected2) =>
    Error(append(expected1, expected2))
  }
}

define report(e: error, position: pos, found: int, stream: &text): text {
  match e {
  | Error of {expected} =>
    let position' = show-position(position) in
    let b on expected = is-empty(expected) in
    if b {
      format("parse error at {}:\nunexpected:\n  {}", [position', show-found(found, stream)])
    } else {
      let expected' = intercalate(*", ", map(show-text-N, expected)) in
      format("parse error at {}:\nexpected:\n  {}\nfound:\n  {}", [position', expected', show-found(found, stream)])
    }
  }
}
