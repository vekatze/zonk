import {
  core.external {malloc},
  core.int {max-int, min-int},
  core.int8-array {_copy-int8-array},
  core.list {is-empty},
  core.magic,
  core.memory {load-int, store-int},
  core.text {format, intercalate, singleton, text-byte-length},
  core.text.show {show-text, show-text-N},
  core.word,
  this.pos {Pos, pos},
}

data observation {
| Rune(rune)
| Range(from: int, size: int, stream: &text)
| Text(text)
| Text-N(&text)
| Label(text)
| End-Of-Input
}

define _observation-size(o: &observation): int {
  case o {
  | Rune(r) =>
    core.rune.size(*r)
  | Range(_, size, _) =>
    *size
  | Text(t) =>
    text-byte-length(t)
  | Text-N(t) =>
    text-byte-length(*t)
  | Label(_) =>
    0
  | End-Of-Input =>
    0
  }
}

define _max-obserwation(o1: observation, o2: observation): observation {
  let s1 on o1 = _observation-size(o1) in
  let s2 on o2 = _observation-size(o2) in
  if gt-int(s1, s2) {
    o1
  } else {
    o2
  }
}

data error {
| Error(
    found: observation,
    expected: list(observation),
    position: pos,
  )
}

define unexpected-end-of-input(p: pos): error {
  Error of {found = End-Of-Input, expected = [], position = p}
}

define _unsafe-slice-N-absolute(start: int, size: int, t: &text): text {
  let start = max-int(start, 0) in
  let size = max-int(size, 0) in
  let ptr = core.magic.cast(&text, int, t) in
  let u = core.word.size in
  let content-ptr = add-int(ptr, add-int(mul-int(u, 2), start)) in
  let to-ptr = add-int(ptr, u) in
  let to = load-int(to-ptr) in
  let new-size = min-int(size, to) in
  let new-ptr = malloc(add-int(new-size, mul-int(u, 2))) in
  store-int(0, new-ptr);
  store-int(new-size, add-int(new-ptr, u));
  let new-content-ptr = add-int(new-ptr, mul-int(u, 2)) in
  _copy-int8-array(add-int(content-ptr, start), new-content-ptr, 0, new-size);
  core.magic.cast(int, text, new-ptr)
}

define show-observation(o: observation): text {
  match o {
  | Rune(r) =>
    show-text(singleton(r))
  | Range(from, len, stream) =>
    _unsafe-slice-N-absolute(from, len, stream)
  | Text(t) =>
    show-text(t)
  | Text-N(t) =>
    show-text-N(t)
  | Label(l) =>
    l
  | End-Of-Input =>
    *"end of input"
  }
}

define show-position(p: pos): text {
  let Pos of {row, column} = p in
  format("row {}, column {}", [%ld(add-int(row, 1)), %ld(add-int(column, 1))])
}

define report(e: error): text {
  let Error of {found, expected, position} = e in
  let position' = show-position(position) in
  let b on expected = is-empty(expected) in
  if b {
    format("parse error at {}:\nunexpected {}", [position', show-observation(found)])
  } else {
    let expected' = intercalate(*", ", map(show-observation, expected)) in
    format("parse error at {}:\nexpected:\n  {}\nfound:\n  {}", [position', expected', show-observation(found)])
  }
}
