import {
  core.int {max-int},
  core.magic,
  core.memory {load-int, store-int},
  core.rune.entity,
  this.point {Point, point},
  this.state {State, state},
}

constant _newline: rune {
  0x0a
}

define _read<a>(ch: &cell(a)): &a {
  let ch = core.magic.cast(&cell(a), int, ch) in
  let val-base = load-int(ch) in
  core.magic.cast(int, &a, val-base)
}

inline _proceed(st-ref: &cell(state), byte-offset: int, new-point: point): unit {
  mutate(st-ref, function (st) {
    let State of {stream, cursor = Point of {}, gap-spot} = st in
    State of {stream = core.text._drop(byte-offset, stream), cursor = new-point, gap-spot}
  })
}

inline _get-new-point(r: rune, row: int, column: int): point {
  if core.rune.entity.as-entity::identity(r, _newline) {
    Point of {row = add-int(row, 1), column = 0}
  } else {
    Point of {row, column = add-int(column, 1)}
  }
}

define _load-text-offset(t: &text): int {
  load-int(magic cast(&text, int, t))
}

define _rewind(t: text, new-from: int): text {
  let from-ptr = core.magic.cast(text, int, t) in
  store-int(new-from, from-ptr);
  core.magic.cast(int, text, from-ptr)
}

inline _rewind-state(base-offset: int, row: int, column: int, gap-spot: int): (state) -> state {
  function (st: state) {
    let State of {stream, cursor = Point of {}} = st in
    let new-stream = _rewind(stream, base-offset) in
    State of {stream = new-stream, cursor = Point of {row, column}, gap-spot}
  }
}

inline _set-gap-spot(st-ref: &cell(state), new-gap-spot: int): unit {
  mutate(st-ref, function (st) {
    let State of {stream, cursor, gap-spot} = st in
    State of {stream, cursor, gap-spot = max-int(gap-spot, new-gap-spot)}
  })
}
